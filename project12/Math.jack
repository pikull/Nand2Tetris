// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack
/**
 *  A library of commonly used mathematical functions.
 *  All functions runs in O(n), where n is the number of bits used
 *  for representing a two's complement integer value (16 in the Hack computer). 
 *  Note: Jack compilers implement multiplication and division
 *  using calls to OS functions in this class.
 */
class Math {
    static int n;             // Number of bits used for representing a two's complement integer
    static Array powersOfTwo; // Stores 2^0, 2^1, 2^2,..., 2^(n-1)

    // Initializes the Math library.
    function void init() {
        let powersOfTwo = Array.new(16);
        let powersOfTwo[0] = 1;
        let powersOfTwo[1] = 2;
        let powersOfTwo[2] = 4;
        let powersOfTwo[3] = 8;
        let powersOfTwo[4] = 16;
        let powersOfTwo[5] = 32;
        let powersOfTwo[6] = 64;
        let powersOfTwo[7] = 128;
        let powersOfTwo[8] = 256;
        let powersOfTwo[9] = 512;
        let powersOfTwo[10] = 1024;
        let powersOfTwo[11] = 2048;
        let powersOfTwo[12] = 4096;
        let powersOfTwo[13] = 8192;
        let powersOfTwo[14] = 16384;
        let powersOfTwo[15] = 16384 + 16384;
        return;
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*'
     *  in an expression, it handles it by invoking this method. 
     *  Thus, in Jack, x * y and Math.multiply(x,y) return the same value. */
    function int multiply(int x, int y) {
        var int result;
        var int shiftedx, absy;
        var int i;
        
        let result = 0;
        let absy = Math.abs(y);
        let shiftedx = Math.abs(x);
        let i = 0;

        while (i < 16) {
            if (Math.bit(i, absy) = 1) {
                let result = result + shiftedx;
            }

            let shiftedx = shiftedx + shiftedx;
            let i = i + 1;
        }
        if (x < 0) {
            let result = -result;
        }
        if (y < 0) {
            let result = -result;
        }

        return result;
    }
    function int bit(int x, int b) {
        if (b & powersOfTwo[x] = 0) {
            return 0;
        }
        return 1;
    }

    /** Returns the integer part of x / y.
     *  When a Jack compiler detects the division operator '/'
     *  an an expression, it handles it by invoking this method.
     *  Thus, x/y and Math.divide(x,y) return the same value. */
    function int divide(int x, int y) {
        var int q, result;
        var boolean neg;
        if (x < 0) {
            let x = -x;
            let neg = ~neg;
        }
        if (y < 0) {
            let y = -y;
            let neg = ~neg;
        }
        if (y > x) { return 0; }

        let q = Math.divide(x, y + y);
        if ((x - ((q + q) * y)) < y) {
            let result = q + q;
        } else {
            let result = q + q + 1;
        }
        if (neg) {
            return -result;
        }
        return result;
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y, j, temp, tempSquared;
        let y = 0;
        let j = 7;
        while (~(j < 0)) {
            let temp = y + powersOfTwo[j];
            let tempSquared = temp * temp;
            if (~(tempSquared > x) & (tempSquared > 0)) {
                let y = temp;
            }
            let j = j - 1;
        }
        return y;
    }

    /** Returns the greater value. */
    function int max(int a, int b) {
        if (a > b) {
            return a;
        }
        return b;
    }

    /** Returns the smaller value. */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        }
        return b;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) {
            return -x;
        }
        return x;
    }
}
